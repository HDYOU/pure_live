pluginManagement {
    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }
    settings.ext.flutterSdkPath = flutterSdkPath()

    includeBuild("${settings.ext.flutterSdkPath}/packages/flutter_tools/gradle")

    repositories {
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/public'}
        maven { url 'https://maven.aliyun.com/repository/google'} //替换google()
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        maven { url 'https://jitpack.io' }
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "7.3.0" apply false
    id "org.jetbrains.kotlin.android" version "1.8.22" apply false
}

include ":app"

gradle.beforeProject { project ->
    if (project.name == "flutter_inappwebview_android") {
        project.afterEvaluate {
            if (project.hasProperty("android")) {
                project.android.buildTypes.release.minifyEnabled = false
            }
        }
    }
    if (project.name == "gsy_video_player") {
        project.afterEvaluate {
            if (project.hasProperty("android")) {
                def gsyVideoVersion  = '9.0.0-release-jitpack'
                project.android.dependencies {
                    implementation "com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-java:$gsyVideoVersion"
                    //是否需要ExoPlayer模式
                    implementation "com.github.CarGuo.GSYVideoPlayer:GSYVideoPlayer-exo2:$gsyVideoVersion"
                    //是否需要AliPlayer模式
                    implementation "com.github.CarGuo.GSYVideoPlayer:GSYVideoPlayer-aliplay:$gsyVideoVersion"
                    //根据你的需求ijk模式的so
                    implementation "com.github.CarGuo.GSYVideoPlayer:gsyVideoPlayer-ex_so:$gsyVideoVersion"
                }
            }
        }
    }
}